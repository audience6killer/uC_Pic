;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;		    SUBRUTINAS DE TIEMPO
;
;	MACROS DE SUBRUTINAS DE TIEMPO DE UNA, DOS Y TRES VARIABLES
;	
;	SE EMPLEAN LOS REGISTROS RAM DE 0X60 HASTA 0X68 PARA ALMACENAR
;	LAS VARIABLES
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    
;   MACRO Y SUBRUTINA DE UNA VARIABLE, CICLOS_MAQ_MAX = 1797
;   SE OCUPA LA DIRECCION 0X60 DE LA RAM PARA GUARDAR EL VALOR DE VAR1
    
SUBT1V	    MACRO	VAR1
	    LOCAL	DECREV1
	    MOVLW	VAR1
	    MOVWF	0X60
DECREV1:    NOP
	    NOP
	    NOP
	    NOP
	    DECFSZ	0X60,F
	    GOTO	DECREV1
	    NOP				; PARA SIMULAR LOS CICLOS DE RETURN Y
	    NOP				; LOS DE CALL
	    NOP
	    NOP
	    ENDM
    
;   MACRO Y SUBRUTINA DE DOS VARIABLES, CICLOS_MAQ_MAX = 459,783
;   SE OCUPA LA DIRECCION 0X61,62 DE LA RAM PARA GUARDAR EL VALOR DE VAR1,
;   VAR2
;   TAMBIEN SE OCUPA LA DIRECCION  0X63 COMO REGISTRO AUXILIAR EN EL DECREMENTO
    
SUBT2V	    MACRO	VAR1,VAR2
	    LOCAL	DECREV2,RECARGAV2
	    MOVLW	VAR1
	    MOVWF	0X61		; VALOR DE MAYOR PESO EN ANIDACION
	    MOVLW	VAR2
	    MOVWF	0X62
RECARGAV2:  MOVF	0X62,W
	    MOVWF	0X63
DECREV2:    NOP
	    NOP
	    NOP
	    NOP
	    DECFSZ	0X63,F
	    GOTO	DECREV2
	    DECFSZ	0X61,F
	    GOTO	RECARGAV2
	    NOP				; PARA SIMULAR LOS CICLOS DE RETURN Y
	    NOP				; LOS DE CALL
	    NOP
	    NOP
	    ENDM


	    
;   MACRO Y SUBRUTINA DE TRES VARIABLES, CICLOS_MAQ_MAX = 117,703,689
;   SE OCUPA LA DIRECCION 0X64,65,66 DE LA RAM PARA GUARDAR EL VALOR DE VAR1,
;   VAR2,VAR3
;   TAMBIEN SE OCUPAN LOS REGISTROS, 0X67 Y 0X68 COMO AUXILIARES
    
VAR1	    EQU		0X64
VAR2	    EQU		0X65
VAR3	    EQU		0X66
AUXV2	    EQU		0X67
AUXV3	    EQU		0X68
		
SUBT3V	    MACRO	V1,V2,V3
	    LOCAL	ST3V, RECARGA3V, DECRE3V
	    MOVLW	V1
	    MOVWF	VAR1		; VALOR DE MAYOR PESO EN ANIDACION
	    MOVLW	V2
	    MOVWF	VAR2
	    MOVLW	V3		; VALOR DE MENOR PESO EN ANIDACION
	    MOVWF	VAR3
ST3V:	    MOVF	VAR2,W		; VAR2	
	    MOVWF	AUXV2
RECARGA3V:  MOVF	VAR3,W		; VAR3	
	    MOVWF	AUXV3
DECRE3V:    NOP				; 
	    NOP
	    NOP
	    NOP
	    DECFSZ	AUXV3,F		; VAR3_AUX
	    GOTO	DECRE3V
	    DECFSZ	AUXV2,F		; VAR2_AUX
	    GOTO	RECARGA3V
	    DECFSZ	VAR1,F		; VAR1
	    GOTO	ST3V
	    NOP				; PARA SIMULAR LOS CICLOS DE RETURN Y
	    NOP				; LOS DE CALL
	    NOP
	    NOP
	    ENDM
	    
	    
	    